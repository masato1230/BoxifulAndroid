plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "com.cookpad.android.plugin.license-tools"
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.jp_funda.boxiful"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField "long", "BUILD_DATE", System.currentTimeMillis() + "L"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    flavorDimensions "advertisement"
    productFlavors {
        staging {
            dimension "advertisement"
            buildConfigField "String", "interstitialUnitId", "\"ca-app-pub-3940256099942544/1033173712\""
        }
        product {
            dimension "advertisement"
            buildConfigField "String", "interstitialUnitId", "\"ca-app-pub-8353659026777346/5564116394\""
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.activity:activity-compose:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Compose Navigation
    implementation "androidx.navigation:navigation-compose:2.4.2"

    // ML Kit Pose Detection
    def ml_kit_pose_detection_version = "18.0.0-beta2"
    implementation "com.google.mlkit:pose-detection:$ml_kit_pose_detection_version"
    implementation "com.google.mlkit:pose-detection-accurate:$ml_kit_pose_detection_version"

    // CameraX
    def camera_x_version = "1.1.0-rc01"
    implementation "androidx.camera:camera-camera2:$camera_x_version"
    implementation "androidx.camera:camera-lifecycle:$camera_x_version"
    implementation "androidx.camera:camera-view:$camera_x_version"
    implementation "androidx.camera:camera-core:$camera_x_version"
    implementation "androidx.camera:camera-view:$camera_x_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-fragment:1.0.0'
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Accompanist
    def accompanist_version = "0.24.7-alpha"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.13.0"

    // OkHttp
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    // GitHubHeatmap
    implementation 'com.github.masato1230:github-heatmap:1.0.2'

    // Splash screen
    implementation 'androidx.core:core-splashscreen:1.0.0-beta02'

    // AdMob
    implementation 'com.google.android.gms:play-services-ads:20.6.0'

    // Play Store In App Review
    implementation 'com.google.android.play:review:2.0.0'
    implementation 'com.google.android.play:review-ktx:2.0.0'
}